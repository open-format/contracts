// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.16;

import {IDefaultOperatorFilterer} from "./IDefaultOperatorFilterer.sol";
import {OPERATOR_FILTER_REGISTRY} from "./DefaultOperatorFiltererConstants.sol";

abstract contract DefaultOperatorFiltererInternal is IDefaultOperatorFilterer {
    function _registerToDefaultOperatorFilterer(address subscriptionOrRegistrantToCopy, bool subscribe)
        internal
        virtual
    {
        // If an inheriting token contract is deployed to a network without the registry deployed, the modifier
        // will not revert, but the contract will need to be registered with the registry once it is deployed in
        // order for the modifier to filter addresses.
        if (address(OPERATOR_FILTER_REGISTRY).code.length > 0) {
            if (!OPERATOR_FILTER_REGISTRY.isRegistered(address(this))) {
                if (subscribe) {
                    OPERATOR_FILTER_REGISTRY.registerAndSubscribe(address(this), subscriptionOrRegistrantToCopy);
                } else {
                    if (subscriptionOrRegistrantToCopy != address(0)) {
                        OPERATOR_FILTER_REGISTRY.registerAndCopyEntries(address(this), subscriptionOrRegistrantToCopy);
                    } else {
                        OPERATOR_FILTER_REGISTRY.register(address(this));
                    }
                }
            }
        }
    }

    /**
     * @dev A helper function to check if an operator is allowed.
     */
    function _checkFilterOperator(address operator) internal view virtual {
        // Check registry code length to facilitate testing in environments without a deployed registry.
        if (address(OPERATOR_FILTER_REGISTRY).code.length > 0) {
            // under normal circumstances, this function will revert rather than return false, but inheriting contracts
            // may specify their own OperatorFilterRegistry implementations, which may behave differently
            if (!OPERATOR_FILTER_REGISTRY.isOperatorAllowed(address(this), operator)) {
                revert DefaultOperatorFilterer_OperatorNotAllowed(operator);
            }
        }
    }
}
